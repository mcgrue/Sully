/// The autoexec function for mountain.map
void Start()
{
    SaveDisable(); //cannot save in dungeons.


    if( flags[F_MNT_DEX_JOIN] )
        entity.x[0]=300000; //if dexter has already joined,
                            //warp his on-map doppleganger way off-screen
                            
    entity.x[1]=300000; //lance's on-map doppleganger is always way off-screen...
        

    if( flags[F_MNT_EVIL_SIGN] ) {
        DestroySign();
    }
    
    if( flags[CHEST_JUJUBE_TRAP] ) {
        SetTile(86, 33, 1, 143);
    }
    
    if( flags[F_MNT_TETRIS_DOOR_OPEN] ) {
        clear_tetris_door_tiles();
    }

/*
    if( flags[F_LAB_BLOWN_UP] )
        DestroyLab();

    if (flags[CHEST_JUJUBE_A]) SetTile(3, 31, 0, 271);
    if (flags[CHEST_JUJUBE_B]) SetTile(5, 31, 0, 271);
    if (flags[CHEST_JUJUBE_C]) SetTile(7, 31, 0, 271);
    if (flags[CHEST_JUJUBE_D]) SetTile(9, 31, 0, 271);
*/

    InitMap();
/*
    if( !flags[F_LAB_BLOWN_UP] && flags[F_LAB_COUNTDOWN] )
        DoLabBlowup();
*/

    play_mountain_music();

    Banner("Mount Jujube",300);
}

void play_mountain_music() { V1_StartMusic( "res/music/DREAMS2.S3M" ); }
void play_lance_music() { V1_StartMusic( "res/music/AMBLE.IT" ); }

void clear_tetris_door_tiles()
{
        SetObs( 34,75, 0 );
        SetObs( 35,75, 35 ); //let's be cute and obstruct just the remaining block.


        SetTile( 35,73, 2, 300 );
        SetTile( 34,74, 2, 300 );
        SetTile( 35,74, 2, 300 ); 
        SetTile( 34,75, 2, 300 ); /// these four tiles get set to the "black" tile if the door is open
            
        SetTile( 35,75, 2, 301 ); /// and the last one gets the last tetris block
}


void tetris_doormat() {
    if( flags[F_MNT_TETRIS_DOOR_OPEN] ) 
        return;
        
    if( CharInParty("Lance") )
    {
        if( !HasItem("Long_Piece") )
        {
            AutoText(T_LANCE,  "This is the door that leads to the Sand Caves, where the dwarf Village is.");
            AutoText(T_DARIN, "Woo caves!" );
            AutoText(T_LANCE,  "...right.");
            AutoText(T_LANCE,  "Anyway, the key is in a chest back up a level." );
            AutoText(T_LANCE,  "I can open it." );  
            AutoText(T_LANCE,  "It's a blue chest.");
            AutoText(T_LANCE,  "Shouldn't be hard to miss.");
            
            playermove( "D3R3W10" );
            
            AutoText(T_LANCE,  "So let's not miss it this time!");
            AutoText(T_DEXTER, "Geez, pushy much?" );
            AutoText(T_LANCE,  "Just eager to visit my fine Dwarven... friends again!");
        }
        else
        {
            PlaySound( sfx_tetris, sfx_volume );
        
            clear_tetris_door_tiles();
            DramaticUseItem( "Long_Piece", 1 );
            
            TextBox(T_LANCE,  "Woo!","Man, those four-pieces never","come when you want.");
            TextBox(T_DEXTER, "Why are so eager to get to this","Dwarf Village, Anyway?.","");
            TextBox(T_LANCE,  "I... forgot my wallet there.","","");
            AutoText(T_DEXTER, "And then the door in got locked and the key put in a booby-trapped chest?" );
            TextBox(T_LANCE,  "Yeah.","","");
            TextBox(T_LANCE,  "...","","");
            AutoText(T_LANCE,  "...so it's going to be a little bit of a monster-filled walk through the caves.  We might want to go get some supplies in Bumsville first.");
            AutoText(T_DARIN, "Woo caves!" );
            AutoText(T_DEXTER, "Woo preparedness!" );
            AutoText(T_LANCE,  "Daw, That's the spirit." );
            
            flags[F_MNT_TETRIS_DOOR_OPEN] = 1;
        }
        
        return;
    }    
    
    if( !flags[F_MNT_TETRIS_DOOR_INTRO] ) {
        
        TextBox(T_DARIN,    "This door is blocked... by blocks?","","");
        
        flags[F_MNT_TETRIS_DOOR_INTRO] = 1;
        
        return;
    }
}


/// exit to the overworld
void to_overworld()
{
    V1_MapSwitch("simpletype_overworld.map", 142,53, TBLACK);
}


void to_station_1()
{
    Warp( 84,79, TCROSS );
}


void to_station_2()
{
    Warp( 15,78, TCROSS );
}

void to_station_3()
{
    Warp( 41, 34, TCROSS );
}

void s3_to_s2()
{
    Warp( 49, 78, TCROSS );
}

void to_summit()
{
    Warp( 185, 61, TCROSS );
}

void from_summit_to_s3()
{
    Warp( 76, 20 TCROSS );
}

void dexter_intro()
{
    if( flags[F_MNT_DEX_JOIN] ) {
        return; // if dexter has already ever joined once, let's never, ever do this again.
    }
    
    MenuOff();
                                    //stop the map-dexter from pacing, and
    EntityMove( 0, "X73Y83W8" );    //move the map-dexter into a proper position.
    
    WaitForEntity( 0 ); //wait for the movescript to stop before continuing...
    
    Entity.Face[0] = FACE_LEFT;
    
    Wait(15);

    TextBox(T_DEXTER,   "Hello? Oh, Darin my good friend!",
                        "It's been such a long time, hasn't it?","");
    TextBox(T_DARIN,    "Dexter! You're the last person I expected",
                        "to find here.","");

    VCPutIMG( "res/images/cells/dexter.gif" ,80,30 );

    TextBoxM(T_DEXTER,  "I left home after my father disapproved",
                        "of my studies. I am searching for a sage.","");
    TextBoxM(T_DEXTER,  "Bumsville no longer gets water from the",
                        "earth, and Rodne has no water from the",
                        "sky.");
    TextBoxM(T_DEXTER,  "I am hoping the sage in these mountains",
                        "may know who is responsible.", "");
    TextBoxM(T_DEXTER,  "Darin, what are you doing here? Don't you",
                        "know that hiking is dangerous?","");
    TextBox(T_DARIN,    "I'm exploring this world. I am on a quest",
                        "to defeat Lord Stan and restore peace.","");
    TextBoxM(T_DEXTER,  "I see... well, it's too risky to tread",
                        "around here by yourself.","");
    TextBoxM(T_DEXTER,  "My investigations have shown some Lord",
                        "Stan activity around this area.","");
    TextBox(T_DARIN,    "Good. Then perhaps I can find some clue or",
                        "lead that will help me in this quest.","");
    TextBox(T_DEXTER,   "Then I'll come with you. You can always",
                        "use some extra help!","");

    ClearVCLayer();

    TextBox(T_DARIN,    "That's a good idea, Dexter.",
                        "Thank you.","");

    // let's get the map-version of Dexter aligned.
    AlignStalker( 0, GetPlayer() );

    // let's wait until the on-map dexter has stopped moving
    WaitForEntity(0);
        
    //...add dexter to the party at level 4 (creating a paty-dexter)...
    JoinPartyAtLevel("Dexter", 4);

    //...and move the map-dexter out into never-never land.
    entity.x[0]=300000;

    //set this event flag.
    flags[F_MNT_DEX_JOIN]=1;
    
    MenuOn();
}

void to_Sand_Cave()
{
    V1_MapSwitch("cave.map", 56,182, TBLACK);
}

void summit_to_sand_cave()
{
    V1_MapSwitch("cave.map", 166,180, TBLACK);
}

void evil_seal_left() {

    if( !flags[F_MNT_BREAK_SEAL] )
    {
        MenuOff();
        
        do_evil_seal_flash();
        
        setPartySpeed( 600 );
        setPartyFrame( 15 );
        playermove( "L3W10" );
        setPartyFacing( FACE_RIGHT );
        setPartyFrame( 0 );
        setPartySpeed( -1 );
        
        do_evil_seal_wom_wom();        
        examine_seal();
        
        MenuOn();
    }
}

void evil_seal_right() {
    if( !flags[F_MNT_BREAK_SEAL] )
    {
        MenuOff();
        
        do_evil_seal_flash();
        
        setPartySpeed( 600 );
        setPartyFrame( 10 );
        playermove( "R3W10" );
        setPartyFacing( FACE_LEFT );
        setPartyFrame( 0 );
        setPartySpeed( -1 );
        
        do_evil_seal_wom_wom();
        examine_seal();     
        
        MenuOn();
    }
}

void do_evil_seal_flash() {
    timer = 0;
    
    if( !Flags[F_MNT_LANCE_JOIN] ) {
        V1_FadeOutMusic( 1 );
    }

    int stage = 0;
    int stage_time = 0;
    
    PlaySound( sfx_wom, 30 );

    while( timer < 50 ) {
        Render();
    
        if( timer >= 0 && stage < 1 ) {
            stage_time = timer;
            stage = 1;
            SoundSplash();
        }
        
        if( stage == 1 || stage == 3 ) {
        
            if( timer <= stage_time+3 ) {
                RectFill( 0,0, ImageWidth(screen), ImageHeight(screen), RGB(255,255,255), screen );
            } else {
                stage++;
            }
        }
        
        if( timer >= 40 && stage == 2 ) {
            stage_time = timer;
            stage = 3;
            SoundSplash();
        }
        
        ShowPage();
    }
}

void do_evil_seal_wom_wom()
{
    int imSeal = LoadImage( "res\images\story_fx\seal.pcx" );
    
    int prevWom = 0;
    
    CutsceneOn();
    cameratracking = 0;
    
    camCtrScrollTo( ((154*16)+8), (58*16), 100 );

    int blitX = 168;// - (ImageWidth(imSeal)/2);
    int blitY = 104;// - (ImageHeight(imSeal)/2);

    timer = 0;

    //scale it in, while rotating.    
    while( timer < 300 ) {
    
        if( ((timer+50)/100) > prevWom ) {
            prevWom++;
            PlaySound( sfx_wom, (10*prevWom)+30 );
        }
    
        Render();
        RotScale( blitX, blitY, systemtime, 100+(timer*3), imSeal, screen );     
        ShowPage();
    }
    
    prevWom++;
    PlaySound( sfx_wom, (10*prevWom)+30 );
    while( timer < 400 ) {
        Render();
        RotScale( blitX, blitY, systemtime, 1000, imSeal, screen );
        ShowPage();
    }

    prevWom++;
    PlaySound( sfx_wom, (10*prevWom)+30 );
    
    /// fade it out at 1:1 size, while rotating.    
    while( timer < 500 ) {
        Render();
        SetLucent(timer-400);
        RotScale( blitX, blitY, systemtime, 1000, imSeal, screen );
        SetLucent(0);
        ShowPage();
    }

    camReturnToPlayer(GetPlayer(), 100);

    cameratracking = 1;
    CutsceneOff();
    
    FreeImage( imSeal );
}

void examine_seal() 
{
    int darin = GetPartyEntity( "Darin" );
    int dexter = GetPartyEntity( "Dexter" );

    if (!flags[F_MNT_LANCE_JOIN]) {
       
        CutsceneOn();
    
        entity.visible[1] = 0; // make Map-Lance invisible.
        
        entity.x[1] = entity.x[dexter];
        entity.y[1] = (entity.y[dexter] - 1); //make invisible map-lance's position dexter's... minus one in the y-direction so he y-sorts behind.
        
        Entity.Face[dexter] = FACE_LEFT; 
        Entity.Face[darin] = FACE_LEFT; //face the waterfall.
        
        Wait(5);

        TextBox( T_DARIN,    
                 "Augh!  Something's pushing us back!"
                 "And there's some kind of evil round",
                 "thing blocking the way!");
        
        TextBoxM( T_DARIN,
                  "But this must be the place! I can see a ",
                  "passage leading into the mountain behind",
                  "the waterfall.");
        
        TextBox( T_DEXTER,   
                 "I see. This is a mystic ward. It seems that",
                 "Lord Stan has anticipated our visit. It", 
                 "will not be easy to penetrate this barrier.");
        
        TextBox( T_DARIN,    
                 "But you know magic!","Can't you de-magic it somehow?","");
                                
        TextBox( T_DEXTER,   
                 "I'm afraid not. My studies have focused",
                 "mainly on the killing side of things.", "");
        
        TextBox( T_DEXTER,                   
                 "Trying to remove an evil seal",
                 "with black magic would be like",
                 "throwing oil on a fire.");
                
        TextBox( T_DARIN,    
                 "Dammit! I can't let something as simple as",
                 "this stop me!",
                 "There must be some way!");
                
        entity.visible[1] = 1;      // make Map-Lance invisible.
        entity.obstructable[1] = 0; //and make him not care about obstructions.
        
        EntityMove(1, "R3W8FW10" );
        
        WaitForEntity( 1 ); //wait for the movescript to stop before continuing...
        
        Entity.Face[1] = FACE_LEFT; //face the map-Lance at the party.
        
        Wait(10);
                
        // Lance steps out from behind Dexter.
        TextBox( T_LANCE,    
                 "I couldn't help but overhear your",
                 "predicament!","");
                 
        cameratracking = 0;
        
        entity.specframe[darin] = 21;
        entity.specframe[dexter] = 21;
        
        EntityMove(darin, "U2D2");
        EntityMove(dexter, "U2D2");
        
        WaitForEntity(darin);
        WaitForEntity(dexter);

        // Darin and Dexter do shock poses.
        TextBox(T_DARIN,    "Where did you come from?","","");
        
        play_lance_music();
        v1_fadeInMusic(1,100);

        entity.specframe[darin] = 0;
        entity.specframe[dexter] = 0;
        
        cameratracking = 1;
        
        TextBoxM(T_LANCE,   
                "There will be time for explanations later.",
                "What's important right now is that you",
                "have a problem and I have a solution!");
                
        TextBoxM(T_LANCE,   
                "Your clever friend here has cut to the",
                "heart of the matter: to undo evil magic,",
                "you need holy magic.");
                
        TextBox(T_LANCE,    
                "It just so happens that there's a powerful",
                "cleric is working in the Dwarf Village",
                "beneath this mountain." );
                
        TextBox(T_LANCE,    
                "A seal like this will be easy for him.", "", "" );
                
        TextBox(T_DARIN,    "Then it is possible!", "But wait...", "Why are you telling us this?");
        
        VCPutIMG( "res/images/cells/lance-intro.png" ,212,20 );
        
        TextBoxM(T_LANCE,   
                "The name's Lance. I'm a sort of freelancer.",
                "I have business in the Dwarf Village, but I",
                "can't get in because the door is blocked.");
                
        TextBox(T_LANCE,    
                "You probably saw the door on your way ",
                "here.", "It's blocked by blocks." );
                
        TextBox( T_LANCE,
                 "To get through, I need an item found",
                 "in a chest nearby. The chest is",
                 "locked, but I can open it." );
                
        TextBoxM(T_DEXTER,  "But how does this involve us?","","");
        
        TextBoxM(T_LANCE,   
                "Aheheh. Oh boy, this is a bit embarrassing.",
                "See, there's a monster inside the chest...","");
                
        TextBox(T_DARIN,    "You need a monster to open a door?", "", "");
        
        TextBox(T_LANCE,    
                "The monster isn't the thing I need!", 
                "It's there to keep people from taking",
                "the item.");
                
        TextBox(T_DEXTER,   
                "So the chest is locked, and then there is a",
                "monster inside the chest?","");            
                
        TextBoxM( T_LANCE,   
                  "Right! Some people, eh?", 
                  "Anyhow, I could use a couple of tough guys like you", 
                  "to help me fight the monster.");
                
        TextBox( T_LANCE,    
                 "The caves leading to the Dwarf Village",
                 "are pretty dangerous, too.","");

        Wait(10);
        
        Entity.Face[dexter] = FACE_RIGHT;
        Entity.Face[darin] = FACE_LEFT;
        
        Wait(30);
        
        // Darin looks at Dexter.
        TextBox( T_DARIN,    "What do you think?", "We'd get to explore a cave.", "");
        TextBox( T_DEXTER,   
                 "Our options are limited.",
                 "I'm not sure we have any",
                 "other choice.");
                
        TextBox( T_LANCE,    
                 "That's the spirit!",
                 "I'm sure we'll be the best of friends.","" );
              
        // let's get the map-version of Lance aligned.
        AlignStalker( dexter, darin );

        // let's wait until the on-map lance has stopped moving
        WaitForEntity(dexter);
        
        // let's get the map-version of Lance aligned.
        AlignStalker( 1, dexter );
        
        // let's wait until the on-map lance has stopped moving
        WaitForEntity(1);

        //...add lance to the party at level 6 (creating a paty-lance)...
        JoinPartyAtLevel("Lance", 6);

        //...and invisible the map-lance away.
        entity.visible[1]=0;
        
        // Lance joins the party.
        flags[F_MNT_LANCE_JOIN] = 1;
        
        ClearVCLayer();                
        play_mountain_music();
        v1_fadeInMusic(100,100);
        
        CutsceneOff();
    }
    else if (!flags[F_MNT_EVIL_SIGN] && !CharInParty("Paxton")) 
    {
        TextBox( T_DEXTER,   
                 "This is truly a powerful seal.",
                 "Lord Stan must be a sorcerer without peer.","");               
    }
}

void trap_chest()
{
    if( flags[CHEST_JUJUBE_TRAP] )
    {
        TextBox(0,  "Empty.","","");                
    }
    else 
    {
        if( CharInParty("Lance") ) 
        {
            if( prompt(T_LANCE, "This is it. You guys ready?","","","No|Yes") ) 
            {
                TextBox(T_DARIN,    "Here it comes!", "", "");
                
                Banner("Battle Happens Here.  Woo.",300);
                
                FindItem( "Long_Piece", 1);
                
                SetTile(86, 33, 1, 143); // sets the trap chest tile to "opened".
                
                // fight monster, get Long Piece item
                TextBox(T_LANCE,    "I knew I could count on you guys!", 
                                    "We make a great team.", "");
                                    
                flags[CHEST_JUJUBE_TRAP] = 1;
            }
            else // if you pick "no," let's remind the player that they still need to do this.
            {
                TextBox( T_LANCE,
                         "Well, we need to fight the monster ", 
                         "in this chest eventually...", "" );
            }
        }
        else
        {
            TextBox(0, "It's locked.","","");              
        }
    }

}



///animates dexter during the RoadBlock() event.
void VictoryDance() {
    int dexter = GetPartyEntity( "Dexter" );
    
    if( flags[F_MNT_EVIL_SIGN]+35 > systemtime ) {
        entity.specframe[dexter] = 45;
    } else {
        if( flags[F_MNT_EVIL_SIGN]+70 > systemtime ) {
            entity.specframe[dexter] = 49;
        } else {
            flags[F_MNT_EVIL_SIGN] = systemtime;
        }
    }
}


///does the roadblock-destruction scene.
void RoadBlock() {
    int darin, dexter; //some temp references for their entity indexes
    int sparklies, pop;
    
    if( !flags[F_MNT_EVIL_SIGN] ) 
    {
        MenuOff();
        
        darin   = GetPartyEntity( "Darin" );
        dexter  = GetPartyEntity( "Dexter" );
        
        sparklies = LoadAnimation( "res/images/story_fx/sparklies.pcx", 16,16);
        pop = LoadAnimation( "res/images/story_fx/pop.pcx", 16,16);
        
        
        TextBox( 0, 
                 "'Evil Lab closed for Evil Renovations'","",
                 "            -The Management");
        
        TextBoxM( T_DARIN,   
                  "Aw, nuts!",
                  "A flimsy sign obstructing the way forward!","");
        
        entity.specframe[darin] = 28;
        wait(50);
        entity.specframe[darin] = 30;
        wait(100);
        
        TextBox( T_DARIN,    
                 "There's no way we can get past this!", 
                 "Crystal!  I've failed you! (sob)","");
                 
        TextBox( T_DEXTER,   
                 "There's no problem that a little", 
                 "WANTON DESTRUCTION couldn't solve!",
                 "Stand back!");
        
        entity.specframe[dexter] = 31; //pose facing forward
        wait(35);
        
        entity.specframe[dexter] = 44; //get out the book
        wait(50);
        entity.specframe[dexter] = 40;
        wait(100);
        
        entity.specframe[dexter] = 34; //flipping the pages
        wait(25);
        entity.specframe[dexter] = 38;
        wait(25);
        entity.specframe[dexter] = 34;
        wait(25);
        entity.specframe[dexter] = 38;
        wait(25);
        entity.specframe[dexter] = 34; 
        wait(25);
        entity.specframe[dexter] = 38;
        wait(25);
        
        entity.specframe[dexter] = 43; //casting frame
        
        //magic sparklie on cane
        SoundMagic2();
        for( timer=0; timer<120; ) {
            Render();
            BlitFrameAt(GetEntScrX(dexter)+1,GetEntScrY(dexter)+6, sparklies, timer/20, screen);    
            ShowPage();
        }
        
        //fire spell, beginning
        SoundBomb();
        for( timer=0; timer<140; ) {
            Render();
            
            BlitFrameAt(GetTileScrX(177),    GetTileScry(64)+6, pop, timer/20, screen);
            BlitFrameAt(GetTileScrX(176)+5,  GetTileScry(65), pop, (timer/20)-2, screen);
            BlitFrameAt(GetTileScrX(177)+4,  GetTileScry(65)+8, pop, (timer/20)-4, screen);
            
            ShowPage();
            
            //let's use the flag as a sfx-counter!
            if( !flags[F_MNT_EVIL_SIGN] ) {
                
                if( timer > 40 ) {
                    SoundBomb();
                    
                    flags[F_MNT_EVIL_SIGN]++;
                }
            } else if( flags[F_MNT_EVIL_SIGN] == 1 ) {
                if( timer > 80 ) {
                    SoundBomb();
                    
                    flags[F_MNT_EVIL_SIGN]++;
                }   
            }
        }
        
        //POW!  Goodbye sign, hello smoking crater!
        DestroySign();
        
        flags[F_MNT_EVIL_SIGN] = systemtime; //using this event's flag as a timer again!
        HookRetrace("VictoryDance");    //let's temporarily hook a map function so 
                                        //dexter dances while Darin gets up and talks! :D
        
        entity.specframe[darin] = 28;
        wait(50);
        entity.specframe[darin] = 0; //set darin back to normal-mode.
        
        TextBox( T_DARIN,    
                 "You know, we probably could've just",
                 "moved it.","");
                            
        HookRetrace("V1RPG_RenderFunc");    //restore the default HookRetrace...
        entity.specframe[dexter] = 0;       //set dexter normal-mode
                            
        TextBox( T_DEXTER,   
                 "What's the point of spending years learning",
                 "the arcane secrets of our universe if",
                 "you're not going to abuse them?" );
        
        //clean up the temp-variables
        FreeAnimation( sparklies ); 
        FreeAnimation( pop ); 
        
        flags[F_MNT_EVIL_SIGN] = 1;
        
        MenuOn();
    } 
}

/// This function takes care of all the tile and obstruction-changing
/// work to remove the sign.
void DestroySign() 
{
    SetObs( 177,64, 0 );
    SetObs( 176,65, 0 );
    
    SetTile( 176,64, 1, 0); // remove the sign from the 
    SetTile( 176,65, 1, 0); // background overlay layer
    SetTile( 177,64, 1, 0);
    SetTile( 177,65, 1, 0);
    
    SetTile( 176,64, 0, 808); // add the charred pit!
    SetTile( 176,65, 0, 828); 
    SetTile( 177,64, 0, 809); 
    SetTile( 177,65, 0, 829);
}