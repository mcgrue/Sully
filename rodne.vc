// Todo in Rodne: implement chests and shops. Make the old-rodne map. Add a special effect when Sara clears the dust blocking the basement. Add other entities.

/// The autoexec function for village.map
/// -s
void start()
{

	SaveDisable(); //cannot save in towns.
		
	InitMap();

	V1_StartMusic("res/music/DOS3L.S3M");

	//don't show the banner when we return from the flashback
	
	if( !flags[F_RODNE_FLASHBACK] )
	{
		Banner("Rodne Town",300);
	}

	//sets all the tiles as they should be.
	rodne_upkeepery();
	
	int i;
	
	for( i=0; i<entities; i++ ) {
		log( str(i)+": "+str( entity.movecode[i] ) );
	}	
}

/// takes care of all general upkeepery.  Called by the map autoexec.
/// @see start()
void rodne_upkeepery()
{

	int i, idx;	
	
	if(flags[F_RODNE_CLEAN_DUST])
	{
		AlterBTile( 53,59,157,2 );
		AlterBTile( 54,59,158,2 );
		AlterBTile( 55,59,159,2 );
		AlterBTile( 53,60,155,2 );
		AlterBTile( 54,60,195,0 );
		AlterBTile( 55,60,156,2 );
	}
	
	// Flashback is ending.
	if( flags[F_RODNE_FLASHBACK] )
	{
		Warp(42,121,TNONE);
		VCScreenFilterOff();
				
		// Face all PCs up
		for (i=0;i<PartySize();i++) { 
			entity.face[GetPartyEntityByIndex(i)] = FACE_UP; }
		
		flags[F_RODNE_FLASHBACK] = 0;
		
		FadeIn(30);
		
		elder_b();
	}	
	if( flags[F_CARROT_QUEST] ) // if you took the carrot, it's gone
	{
		SetTile(19,10,1,0);
	}
	// If the lab is destroyed and you haven't taken the carrot yet, it's starting to grow.
	else if (flags[F_LAB_BLOWN_UP]) 
	{
		SetTile(19,10,1,35);
		RevealWater();
	}
	else { ConcealWater(); }
	
	if (flags[F_RODNE_CLEAN_DUST]) cleardust();
}

void RevealWater()
{
	curmap.rstring = "1,2,5,E,3,4,R";
}

void ConcealWater()
{
	curmap.rstring = "1,2,E,3,4,R";
}

/// zone.  exit to overworld.map.
void n_exit() /* 1 */
{
	v1_mapswitch("simpletype_overworld.map",89,112,TBLACK);
}

/// zone.  exit to overworld.map (love shack area).
void e_exit() /* 2 */
{
	v1_mapswitch("simpletype_overworld.map",91,114,TBLACK);
}

/// entity
void villager_a() /* 3 */
{
	EntStart();
	
	TextBox(0,	"Welcome to the town of Rodne.",
				"Yes, I'm the dork who says the town name",
				"and I'm proud of it!");
			
	EntFinish();
}

/// entity
void villager_b() /* 4 */
{
	EntStart();
	
	if( flags[F_CARROT_QUEST] )
	{
		TextBox(0,	"I... I don't believe it!",
					"Please use the power of the",
					"carrot to destroy Lord Stan.");
					
		EntFinish();
		return;
	}
	

	TextBoxM(0,	"Lord Stan has cursed our sky so we never ",
				"have any rain.",
				"Only this carrot can grow.");
			
	TextBox(0,	"We all pray that one day it will grow to",
				"be the Sacred Carrot of Ultimate Power.", "");

	if( CharInParty("Dexter") )
	{
		TextBox(T_DEXTER,	"The chance that this root will grow into an",
							"omnipotent holy weapon is exceedingly",
							"remote.");
				
		TextBox(T_DARIN,	"Don't be such an egghead, Dex.","","");
	}
	
	EntFinish();
}

/// entity
void villager_c() /* 5 */
{
	EntStart();
	
	TextBox(0,	"If it's a technician you're looking for,",
				"the lab is in the southwest area of the",
				"village.");
	TextBox(0,	"Sara doesn't understand that we live in",
				"simple happiness without evil technology.","");
	

	if( CharInParty("Sara") )
	{
		TextBox(T_SARA,"Hey!","","");
	}
	
	EntFinish();
}

/// entity
void villager_d() /* 6 */
{
	EntStart();
	
	TextBox(0,	"This is the elder's place.",
				"He will teach you about the heritage of",
				"our village.");
	

	if( CharInParty("Dexter") )
	{
		TextBox(3,"Oh, boy! Learning!","","");
	}
	
	EntFinish();
}

/// entity
void villager_e() /* 7 */
{
	EntStart();
	
	TextBoxM(0,	"We live in fear of both Lord Stan and",
				"Big Daddy Bubba.",
				"Lord Stan resides in Castle Heck.");
	TextBoxM(0,	"Castle Heck is the treacherous looking",
				"place on the shores of the eastern",
				"peninsula.");
	TextBox(0,	"Big Daddy Bubba is a reclusive hermit",
				"who captures people for his love shack",
				"in the Forest.");
	

	if( CharInParty("Sara") )
	{
		TextBox(2,	"Yes, Bubba is a big stinker.",
					"We shall also beat Lord Stan",
					"and his evil lackey, Galfrey!");
				
		if( charInParty("Galfrey") )
		{
			TextBox(5,"Hey, that hurts...","","");
		}
	}
	
	EntFinish();
}

/// entity
void villager_f() /* 8 */
{
	EntStart();
	
	TextBox(0,	"There's a trail leading deep into the Forest on",
				"the east edge of the village.", "");
				
	TextBox(0,	"If you go, be sure to steer clear of",
				"Big Daddy Bubba's love shack.", "");
				
	

	if( CharInParty("Sara") && CharInParty("Crystal") )
	{
		TextBox(T_SARA,	"Love shack... I bet you and Darin plan on",
					"spending some time there, eh, Crystal?", "");
		TextBox(T_CRYSTAL,	"Don't make me scratch your eyes out.", "","");
	}
	
	EntFinish();
}

/// zone
void rabbit() /* 9 */
{
	EntStart();
	
	if( flags[F_CARROT_QUEST] )
	{
		TextBox(T_BUNNY,	"You... monster! You absolute",
							"monster! How you could you rob",
							"me of my sole passion in life?");		
					
		EntFinish();
		return;
	}
	
	TextBox(T_BUNNY,	"My sole purpose in life is to eat that",
						"sacred carrot, but that goober keeps",
						"guarding it.");
	TextBox(T_BUNNY,	"Sooner or later, he will grow sleepy.",
						"I shall bide my time, then I shall strike!","");
				
			
	if( CharInParty("Crystal") )
	{
		TextBox(4,	"What an adorable little",
				"rabbit. I shall pet it.","");

		if( CharInParty("Sara") && CharInParty("Galfrey") )
		{
			TextBox(2,"Do you think rabbits are",
				"cute, Galfrey?","");
			
			TextBox(5,"Not really.","","");
		}
	}
	
	EntFinish();
}

/// zone
void bird() /* 10 */
{
	EntStart();
	
	TextBox(T_BIRD,	"This trail leads deep into the Forest.",
					"Enjoy the serene beauty of the wind",
					"and trees.");
	TextBox(T_BIRD,	"Umm... er... I mean...",
					"*CHIRP*!","");
			
	if( CharInParty("Dexter") )
	{
		TextBox(3,"What a remarkable bird.","","");
	}
	
	EntFinish();
}

/// zone
void warning_sign() 
{
	EntStart();
	
	TextBox(0,	"WARNING!",
			"The forest is dangerous! Stay out!","");
			
	if( CharInParty("Galfrey") )
	{
		TextBox(T_GALFREY,	"You can't tell me what to do!",
						"You're just a sign.","");
	}
	
	EntFinish();
}


/// entity
void elder_a() /* 11 */
{
	EntStart();
	
	TextBox(0,	"The village elder is upstairs.",
			"He remembers the early days of this",
			"crappy little village.");
	if( CharInParty("Sara") )
	{
		TextBox(T_SARA,	"I'd really rather you not know",
					"about this, Darin.","");

		if( CharInParty("Crystal") )
		{
			TextBox(T_CRYSTAL,"What's the deal, Darin? Juicy",
						"secrets about Sara's sordid",
						"past? I must know!");
		}
	}
	
	EntFinish();
}

/// entity
void elder_b() /* 12 */
{
	EntStart();
	
	if( CharInParty("Sara") )
	{
		TextBox(0,	"Good luck, young folks. Enjoy",
					"your stay here in Rodne.","");
		
		EntFinish();
		return;
	}
	
	if( flags[F_RODNE_FLASHBACK] )
	{
		TextBox(0,	"Good luck, young folks. Enjoy",
					"your stay here in Rodne.","");
		
		EntFinish();
		return;
	}
	
	if( !flags[F_RODNE_FLASH_OVER] && !CharInParty("Sara") )
	{
		TextBoxM(0,	"I am the village elder.",
					"Have a seat and I will tell you about the",
					"ancestry of Rodne.");
		
		TextBoxM(0,	"It all began when a young engineer named ",
					"Sara started a laboratory in the Forest.", "");
		TextBox(0,	"Her machine to clone rats exploded and",
					"created all the clone people you see here.", "");
	
		
		
		VCCustomFilter( RGB(0,0,0), RGB(255,128,64) );
		VCScreenFilterLucent( 20 );
		
		
		MenuOff();

		RemovePlayer( "Dexter" );	//
		RemovePlayer( "Darin" );	//remove the boys for the flashback!
		
		
		JoinParty("Sara");	//It's Peanut Butter Sara Time!
		FullHeal(IsCharacter("Sara"));

		Flags[F_RODNE_FLASHBACK] = 1;
	
		v1_Mapswitch("OLDVILLE.MAP",40,10,0);
	}
	
	TextBoxM(0,	"So you see, it's not because of a lazy",
				"sprite artist, but rather it's scientific.", "");
				
	TextBox(0,	"Sara still lives here,",
				"designing her machines and making the",
				"world spiffier.");
	
	
	EntFinish();
}

/// entity
void blacksmith() /* 13 */
{
	EntStart();
	
	TextBox(0,	"I am Thor, the blacksmith.",
				"Well met, travelers! What",
				"can I do for you today?");
	
	SetSellEquipmentShop(1);
	SetSellSupplyShop(0);
	MenuShop("Sting_Whip,Lead_Wrench,Iron_Sword,Steel_Lance,Bracer,Buckler,Tower_Shield,Cloak,Robe,Titanium_Suit,Tiara,Head_Brace");
	

	TextBox(0,	"I thank you, sir! Good day.","","");
	
	EntFinish();
}

/// zone
void pharmacy() /* 14 */
{
	EntStart();
	
	TextBox(0,	"You look like a nice bunch of kids. ",
				"I'll show you the real medicine, not just", "Tic-Tacs.");
	
	SetSellEquipmentShop(0);
	SetSellSupplyShop(1);

	MenuShop("Herb,Medicine,Miracle_Brew,Starlight,Fury_Ring,Protect_Locket");
	
	TextBox(0,	"Aww... come on!",
				"Buy one more medicine, please?","");
				
	EntFinish();
}

/// zone
void rat() /* 15 */
{
	EntStart();
	
	if( flags[F_LOVE_SARA_JOIN] || flags[F_RODNE_TALKRAT] )
	{
		TextBox(T_SLASHER,		"Seeya, guys!",
							"Have fun defeating Lord Stan!","");

		if( CharInParty("Sara") )
		{
			TextBox(T_SARA,		"I'll be back soon, Slasher,",
							"and I'll be sure to pick up",
							"some sunflower seeds.");
		}
		else if (CharInParty("Galfrey"))
		{
			TextBox(T_GALFREY,	"Defeating people is ALWAYS,",
							"fun.","");			
		}
		
		EntFinish();
		return;
	}
	
	//Do this if Sara hasn't joined yet.
	if( !flags[F_LOVE_SARA_JOIN] )
	{
		TextBox(T_SLASHER,	"Squeak! Squeak!","","");
		
		TextBox(T_DARIN,	"How terrible! They're enslaving and",
							"experimenting on poor helpless animals!", "");
					
		TextBoxM(T_SLASHER,	"No, actually Sara treats me well, except I",
							"don't have any friends.", "");
					
		TextBox(T_SLASHER,	"After Sara's machine blew up, she was",
							"unable to clone any more cute little rats.", "");
		
		TextBox(T_DEXTER,	"Sara?",
							"Yes, we need to speak to her.",
							"Where is she?");
					
		TextBox(T_SLASHER,	"She went into the Forest to find some",
							"wood for her new machine.", "");
					
		TextBox(T_DARIN,	"Well, we can't wait for her to get back.",
							"Dexter, let's go into the Forest to", "find her.");
					
		TextBox(T_DEXTER,	"Agreed.",
							"Thank you, cute little rat.","");
	}
	
	flags[F_RODNE_TALKRAT] = 1;
	
	EntFinish();
}

/// warp
void elder_enter() /* 16 */
{
	warp( 16,131, TCROSS );
	Banner( "Inn",100 );
}

/// warp
void weap_enter() /* 17 */
{
	warp( 57,104, TCROSS );
	Banner( "Blacksmith",100 );
}

/// warp
void item_enter() /* 18 */
{
	warp( (entity.x[GetPlayer()]/16)+33,103, TCROSS );
	Banner( "Pharmacy",100 );
}

/// warp
void magic_enter() /* 18 */
{
	warp( 108,104, TCROSS );
	Banner( "Magic Shop",100 );
}

/// warp
void sara_enter() /* 19 */
{
	warp( (entity.x[GetPlayer()]/16)-3,105, TCROSS );
	Banner( "Sara's House",100 );
}

/// open door, then warp
void Shed_Enter() /* 29 */
{
	// If door isn't open, open it
	if(getObs(event.tx,event.ty)) 
	{ 
		SetTile(event.tx,event.ty, 1, 0);
		SetTile(event.tx,event.ty-1, 1, 0);		
		SetObs(event.tx,event.ty, 0);		
		SoundOpenDoor();	
	}
	// Else warp
	else { warp( 38, 98, TCROSS ); }
}

/// warp
void elder_exit() /* 20 */
{
	warp(16,31, TCROSS);
}

/// warp
void elder_upstair_left() /* 21 */
{
	warp( 38,130, TCROSS );
}

/// warp
void elder_upstair_right() /* 21 */
{
	warp( 53,130, TCROSS );
}

/// warp
void elder_downstair_left() /* 22 */
{
	warp( 9,130, TCROSS );
}

/// warp
void elder_downstair_right() /* 22 */
{
	warp( 24,130, TCROSS );
}

// Remove away the sawdust blocking Sara's basement
void cleardust()
{
	SetTile(7,94,1,0);
	SetTile(8,94,1,0);
	SetTile(9,94,1,0);
	SetTile(7,95,1,0);
	SetTile(8,95,1,0);
	SetTile(9,95,1,0);
	SetObs(7,95,0);
	SetObs(8,95,0);
	SetObs(9,95,0);
}

/// the dust-revealing event, and after that, a warp.
void Basement_Down() /* 25 */
{
	if (!flags[F_RODNE_CLEAN_DUST]) 
	{
		
		if( CharInParty("Sara") )
		{
			TextBoxM(T_SARA,			"This leads to my basement.",
								"Let me remove the sawdust",
								"that conceals the passage.");

			cleardust();
			// There should be some sort of effect here.

			TextBox(T_SARA,	"Please feel free to enter.",
							"The Activator is in my",
							"storage room below.");

			flags[F_RODNE_CLEAN_DUST] = 1;
			return;
		}
		else 
		{
			TextBox(T_DARIN,			"It's a huge pile of sawdust.",
								"It smells like shop class.","");			
		}
	}
	else
	{
		warp( 67,119, TCROSS );
	}
}

/// warp
void Basement_Up() /* 26 */
{
	warp( 8,95, TCROSS );
}

/// warp
void Weap_Exit() /* 27 */
{
	warp( 46,32, TCROSS );
}

/// warp
void Item_Exit() /* 28 */
{
	warp( (entity.x[GetPlayer()]/16)-33,17, TCROSS );
}


/// warp
void Magic_Exit() /* 28 */
{
	warp( 47,49, TCROSS );
}

/// warp
void Sara_Exit() /* 29 */
{
	warp( (entity.x[GetPlayer()]/16)+3,50, TCROSS );
}

/// warp
void Shed_Exit() /* 29 */
{
	warp( 11, 41, TCROSS );
}

/// Treasure: Medicine
///
/// @see OpenTreasure()
/// @see FindItem()
void Chest_A() /* 30 */
{
	if( OpenTreasure(CHEST_RODNE_A, 51,81, 177, 0 ) )
	{
		FindItem( "Medicine", 1 );
	}
}

/// Treasure: Gold_Helmet
///
/// @see OpenTreasure()
/// @see FindItem()
void Chest_B() /* 31 */
{
	if( OpenTreasure(CHEST_RODNE_B, 51,85, 177, 0 ) )
	{
		FindItem( "Gold_Helmet", 1 );
	}
}

/// Treasure: Thermal_Activator
///
/// @see OpenTreasure()
/// @see FindItem()
void Chest_C() /* 32 */
{
	if( OpenTreasure(CHEST_RODNE_C, 53,83, 177, 0 ) )
	{
		FindItem( "Thermal_Activator", 1 );
	}
}

/// Treasure: Blur_Ring
///
/// @see OpenTreasure()
/// @see FindItem()
void Chest_D() /* 33 */
{
	if( OpenTreasure(CHEST_RODNE_D, 55,81, 177, 0 ) )
	{
		FindItem( "Blur_Ring", 1 );
	}
}

/// Treasure: Blur_Ring
///
/// @see OpenTreasure()
/// @see FindMoney()
void Chest_E() /* 34 */
{
	if( OpenTreasure(CHEST_RODNE_E, 55,85, 177, 0 ) )
	{
		FindMoney( 450 );
	}
}


/// Does tilesetting upkeep for Rodne.
///
/// @param flag_idx the flags[] index to check for this treasure chest
/// @param tx coordinate of the chest
/// @param ty coordinate of the chest
void RodneMasterChestCleanup( int flag_idx, int tile_x, int tile_y ) 
{
	if( flags[flag_idx] )
	{
		AlterBTile( tile_x,tile_y, 177,2 );
	}
}

/// The Sacret Carrot of Ultimate Power!
void Carrot() /* 39 */
{
	int save_vol;
	if (!flags[F_CARROT_QUEST]) // if carrot is still there
	{
		save_vol = V1_GetCurVolume(); //save the current volume to restore later

		EntStart();

		if( HasItem("Pearl_of_Truth") && !flags[F_CARROT_QUEST])
		{
			TextBox(1,	"This is strange... the [Pearl of Truth]",
						"is glowing brighter when I hold it down",
						"here.");

			WhiteOut(100);

			AlterBTile(13,6,89,2);

			WhiteIn(100);

			TextBox(1,	"Wow! The Pearl has lifted the",
						"[Sacred Carrot of Ultimate Power] from",
						"the earth!");

			V1_FadeOutMusic( 100 );

			SoundFanfare();

			FindItem("Sacred_Carrot",1);
			Wait( 500 );

			flags[F_CARROT_QUEST] = 1; //first step in the CARROT QUEST!

			V1_StartMusic("res/music/DOS3L.S3M");
			V1_FadeInMusic( 100, save_vol );//restore the volume

		}

		V1_SetCurVolume(save_vol);

		EntFinish();
	}
}


/// Zone
void Machine()
{
	EntStart();
	
	TextBox(T_DARIN,	"What is this monstrosity?",
						"It looks like a mangled wreck",
						"of pipes.");
         
	if( CharInParty("Sara") )
	{
		TextBox(T_SARA,	"I'm *trying* to re-build it,",
						"thank you very much. That's",
						"why I needed the wood.");
	}
	
	EntFinish();
}

void DayClock()
{
	EntStart();
	TextBox(T_DARIN, "Ooh, shiny.","","");
	if (1 || CharInParty("Sara"))
	{
		TextBoxM(T_SARA,	"It's a day clock. It tells you whether",
					"it's daytime or nighttime.","");
		TextBox(T_SARA,	"Right now it's daytime.","","");
		TextBox(T_DARIN, "Wow!","","");
	}
	EntFinish();
}

void hidden_rabbit()
{
	EntStart();
	TextBox(0,		"I bet you think you're pretty clever for",
				"looking back here, right?",
				"Well, I'm not impressed!");
	EntFinish();				
}

void lone_post()
{
	EntStart();
	TextBox(T_DARIN,	"It's a fencepost. ",
				"I wonder what it's doing here?","");
	EntFinish();					
}